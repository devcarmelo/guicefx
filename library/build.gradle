plugins {
    id 'java-library'
    id 'maven-publish'
    id 'signing'
    id 'org.openjfx.javafxplugin' version '0.0.10'
}

group = 'io.github.devcarmelo'
archivesBaseName = 'guicefx'
version = '1.0-SNAPSHOT'

sourceCompatibility = '11'
targetCompatibility = '11'

java {
    withJavadocJar()
    withSourcesJar()
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifactId = 'guicefx'
            from components.java
            versionMapping {
                usage('java-api') {
                    fromResolutionOf('runtimeClasspath')
                }
                usage('java-runtime') {
                    fromResolutionResult()
                }
            }
            pom {
                name = 'Guice FX'
                description = 'JavaFX DI with Guice library'
                url = 'https://github.com/devcarmelo/guicefx'
                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                developers {
                    developer {
                        id = 'devcarmelo'
                        name = 'Carmelo M.A'
                        email = 'carmelo.freelancero@gmail.com'
                    }
                }
                scm {
                    connection = 'scm:git:git://github.com/devcarmelo/guicefx.git'
                    developerConnection = 'scm:git:ssh://github.com:devcarmelo/guicefx.git'
                    url = 'https://github.com/devcarmelo/guicefx/tree/master'
                }
            }
        }
    }
    repositories {
        maven {
            // change URLs to point to your repos, e.g. http://my.org/repo
            def releasesRepoUrl = layout.buildDirectory.dir('repos/releases')
            def snapshotsRepoUrl = layout.buildDirectory.dir('repos/snapshots')
            url = version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl
        }
    }
}

signing {
    sign publishing.publications.mavenJava
}


javadoc {
    if(JavaVersion.current().isJava9Compatible()) {
        options.addBooleanOption('html5', true)
    }
}

repositories {
    mavenCentral()
}

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}

javafx {
    version = '11.0.2'
    modules = ['javafx.controls', 'javafx.fxml']
}

dependencies {
    implementation 'com.google.inject:guice:5.1.0'
}
